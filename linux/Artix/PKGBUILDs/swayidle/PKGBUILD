# Taken from: https://aur.archlinux.org/cgit/aur.git/plain/PKGBUILD?h=swayidle-git
pkgname=swayidle
pkgver=1.6
pkgrel=99   # override versions from repos
license=('MIT')
pkgdesc='Idle management daemon for Wayland'
makedepends=(
    'meson'
    'scdoc'
    'wayland-protocols'
    'git'
)
depends=('wayland')
arch=('x86_64')
url="https://github.com/swaywm/swayidle"
source=('git+https://github.com/swaywm/swayidle.git')
sha1sums=('SKIP')
provides=('swayidle')
conflicts=('swayidle')

pkgver() {
    cd "$pkgname"
    git describe --tags --abbrev=0
}

build() {
    meson "$pkgname" build \
        -Dlogind=disabled \
        --prefix /usr \
        --buildtype=plain
    ninja -C build
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "$pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 "$pkgname/README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: set et nowrap sw=4 ts=4 sts=4:
